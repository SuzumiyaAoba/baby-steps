plugins {
  id 'base'
  id 'com.diffplug.spotless' version '6.25.0' apply false
}

group = 'babysteps'
version = '0.1.0-SNAPSHOT'

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'com.diffplug.spotless'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    compileOnly 'org.jspecify:jspecify:0.3.0'
    testCompileOnly 'org.jspecify:jspecify:0.3.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
  }

  spotless {
    java {
      googleJavaFormat('1.17.0')
      removeUnusedImports()
    }
    groovyGradle {
      greclipse()
      indentWithSpaces(2)
    }
  }

  tasks.named('build') {
    dependsOn 'spotlessApply'
  }
}
