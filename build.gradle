plugins {
  id 'base'
  id 'com.diffplug.spotless' version '6.25.0' apply false
}

group = 'babysteps'
version = '0.1.0-SNAPSHOT'

tasks.register('jacocoTestReport') {
  dependsOn(subprojects.collect { it.tasks.named('jacocoTestReport') })
}

subprojects {
  apply plugin: 'java-library'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'jacoco'

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(resolveToolchainVersion())
    }
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    compileOnly 'org.jspecify:jspecify:0.3.0'
    testCompileOnly 'org.jspecify:jspecify:0.3.0'
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.25.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  }

  test {
    useJUnitPlatform()
    if (!isJacocoSupported()) {
      jacoco.enabled = false
    }
    finalizedBy jacocoTestReport
  }

  jacoco {
    toolVersion = '0.8.12'
  }

  jacocoTestReport {
    dependsOn test
    onlyIf { isJacocoSupported() }
    reports {
      xml.required = true
      html.required = true
      csv.required = false
    }
  }

  spotless {
    java {
      googleJavaFormat('1.17.0')
      removeUnusedImports()
    }
    groovyGradle {
      greclipse()
      indentWithSpaces(2)
    }
  }

  tasks.named('build') {
    dependsOn 'spotlessApply'
  }
}

def resolveToolchainVersion() {
  def ciValue = providers.environmentVariable('CI')
  if (ciValue.isPresent()) {
    return 21
  }
  return JavaLanguageVersion.current().asInt()
}

def isJacocoSupported() {
  return JavaVersion.current().majorVersion.toInteger() <= 22
}
